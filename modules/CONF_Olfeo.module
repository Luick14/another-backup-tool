#!/usr/bin/perl -w

# $Id: CONF_Olfeo.module 483 2012-04-10 09:50:19Z marc@mguyard.com $

use strict;
use warnings;
use FindBin;
use lib "$FindBin::Bin/../libs";
use ABT::Global;
use ABT::Modules;
use Getopt::Long;
use Pod::Usage;
use Switch;
use Net::OpenSSH;

###################
# Script Variable #
###################

my $module_device = "[CONF] Olfeo";
my $module_description = "Module Olfeo (Backup by SSH)";
my $author = 'Marc GUYARD <m.guyard@orange-ftgroup.com>';
my $version = '0.1';

#####################
# Default arguments #
#####################

my $GenConfigFile;
my $ConfigFile;
my $show_help;
my $show_man;
my $default_retention = "15";

#############
# Functions #
#############

## Function who show the module short description
sub description {
	print $program."\n	".$module_device." - ".$module_description." (version : ".$version.").\n\n";
}

# Function who verify the olfeo type
sub olfeo_type {
	my $olfeo_type = $_[0];
	switch ($olfeo_type) {
		case "APPLIANCE" { return 1; }
		case "LINUX" { return 1; }
		else {
			$logger->error("The Olfeo type entered (".$olfeo_type.") isn't valid (APPLIANCE or LINUX)");
			return 0;
		}
	}
}

## Function who generate a XML configuration file
sub generate {
	$logger->debug("[".$GenConfigFile."] Generating configuration file ") if $verbose;
	# Question function : 
	#	ARG1 : Question
	# 	ARG2 : Default Reply
	#	ARG3 : Reply is mandatory ? (1 => YES / 0 = NO)
	#	ARG4 : Function to verify the reply conformity
	my $device_ip_address = &questions("Please enter the Device IP to backup", "", "1", "verify_ip");
	my $device_description = &questions("Please enter the description of the device", "", "1", "");
	my $username = &questions("Please enter the device username", "root", "1", "");
	my $certificate = &questions("Please enter the SSH certificate (leave empty to use the default certificate)", "", "0", "");
	my $olfeo_type = &questions("Your Olfeo is an appliance or linux install (APPLIANCE/LINUX) - case sensitive", "appliance", "1", "main::olfeo_type");
	my $store_folder = &questions("Please specify the folder who store the backup", "", "1", "verify_folder");
	my $file_prefix = &questions("Please enter the backup file prefix (date automaticly add)", "", "1", "");
	my $retention = &questions("Please enter configuration retention in number of save backup (leave empty to use the default retention : ".$default_retention.")", "", "0", "verify_retention");
	my %XML = (
		'enable' => '1',
		'module' => $program,
		'date' => $date,
		'ip' => $device_ip_address,
		'description' => $device_description,
		'username' => $username,
		'certificate' => $certificate,
		'olfeo-type' => $olfeo_type,
		'store-folder' => $store_folder,
		'file-prefix' => $file_prefix,
		'retention' => $retention,
		); ## To Update with all information need
	my $Config = new XML::Simple (NoAttr=>1, NoSort => 1, KeepRoot => 1);
	my $Config_data = $Config->XMLout({config=>\%XML}, outputfile=>$GenConfigFile, NoAttr=>1);
	$logger->info("[".$GenConfigFile."] Configuration generation finish... ");
}

## Function who backup device
sub backup {
	$logger->debug("[".$ConfigFile."] Starting backuping configuration ") if $verbose;
	# Declare all configuration from the XML file
	my $config_parsing = &parse_config($ConfigFile);
	my $device_ip_address = $config_parsing->{'ip'};
	my $username = $config_parsing->{'username'};
	my $certificate = $config_parsing->{'certificate'};
	my $olfeo_type = $config_parsing->{'olfeo-type'};
	my $store_folder = $config_parsing->{'store-folder'};
	my $file_prefix = $config_parsing->{'file-prefix'};
	my $retention = $config_parsing->{'retention'};
	# Test if $retention is empty. If empty, use $default_retention
	$retention = $default_retention if ( $retention eq "" );
	my $configuration_filename = $store_folder."/".$file_prefix."_".$date.".tgz";
	# Backup
	$Net::OpenSSH::debug = -1 if $verbose;
	my $ssh;
	if ( $certificate ) {
		$ssh = Net::OpenSSH->new($device_ip_address,
			user		=> $username,
			key_path	=> $certificate,
			kill_ssh_on_timeout => 1,
			timeout		=> 1800,
		);
	} else {
		$ssh = Net::OpenSSH->new($device_ip_address,
			user		=> $username,
			kill_ssh_on_timeout => 1,
			timeout		=> 1800,
		);
	}
	$ssh->error and &return_code(50, "Couldn't establish SSH connection : " . $ssh->error);
	# Generate backup
	my $stderr = 1;
	$stderr = 0 if $verbose;
	if ($olfeo_type eq "LINUX" ) {
		$ssh->system({ stderr_discard => $stderr }, "chroot /opt/olfeo5/chroot/ && mysqldump olfeo_db5 | gzip > /opt/olfeo5/olfeo_db5.sql.gz && mysqldump olfeo_stats5 | gzip > /opt/olfeo5/olfeo_stats5.sql.gz");
			$ssh->error and &return_code(50, "Backup SQL db5 failed : " . $ssh->error);
	} else {
		$ssh->system({ stderr_discard => $stderr }, "mysqldump olfeo_db5 | gzip > /opt/olfeo5/olfeo_db5.sql.gz && mysqldump olfeo_stats5 | gzip > /opt/olfeo5/olfeo_stats5.sql.gz");
			$ssh->error and &return_code(50, "Backup SQL stats5 failed : " . $ssh->error);
	}
	$ssh->system({ stderr_discard => $stderr }, "tar --ignore-failed-read  -C / -czf /opt/olfeo5/sauvegarde.tar.gz --exclude '/opt/olfeo5/data/log' /opt/olfeo5/olfeo_db5.sql /opt/olfeo5/olfeo_stats5.sql /opt/olfeo5/local/template/ /opt/olfeo5/data/ /etc/");
		$ssh->error and &return_code(50, "Backup configuration failed : " . $ssh->error);
	# SCP download
	$ssh->scp_get('/opt/olfeo5/sauvegarde.tar.gz', $configuration_filename);
		$ssh->error and &return_code(50, "SCP Configuration failed : " . $ssh->error);
	# Delete backup on device
	$ssh->system({ stderr_discard => $stderr, timeout => 120 }, "rm /opt/olfeo5/sauvegarde.tar.gz /opt/olfeo5/olfeo_db5.sql.gz /opt/olfeo5/olfeo_stats5.sql.gz");
		$ssh->error and &return_code(50, "Backup remove failed : " . $ssh->error);
	# Purge old backup
	&purge_files($retention, $store_folder, "^".$file_prefix."_.*");
}

##########
# Script #
##########

# Check If arguments are present
if ( @ARGV > 0 ) {
	# Parse Arguments
	GetOptions( 	
		"d|description" => \&description,
		"c|config=s" => \$ConfigFile,
		"g|generate=s" => \$GenConfigFile,
		"v|verbose" => \$verbose,
		"q|quiet" => sub { $verbose = 0 },
		"man" => \$show_man,
		"h|help|?" => \$show_help 
	)
	# Show usage if no argument match
	or pod2usage({-message => "Argument unknown\n", -exitval => 1});
} else {
	# Show usage if no argument specified
	pod2usage({-message => "No argument specify\n", -exitval => 2});
}

# Show help usage
pod2usage(1) if $show_help;
# Show man usage
pod2usage(-verbose => 2) if $show_man;

# Call functions
&generate($GenConfigFile) if $GenConfigFile;
&backup($ConfigFile) if $ConfigFile;


__END__

=head1 NAME

ABTM - Another Backup Tool Module

=head1 AUTHOR

Script written by Marc GUYARD for Orange NIS <m.guyard@orange-ftgroup.com>.

=head1 VERSION

0.1 BETA PERL

=head1 SYNOPSIS

	Options:
		--description
			show the module description
		--config <configuration_file>
			use a configuration file specified
		--generate <configuration_file>
			generate a configuration file specified
		--verbose
			active script verbose
		--quiet
			active script quiet mode
		--man
			full documentation
		--help
			brief help message

=head1 OPTIONS

=over 8

=item B<--description>

Show the module description

=item B<--config>

Backup using a configuration file specify in argument.

=item B<--generate>

Generate a configuration file with the path and the name specify in argument.

=item B<--verbose>

Activate verbose mode. Should be used with another argument.

=item B<--quiet>

Activate quiet mode. Should be used with another argument.

=item B<--help>

Print a brief help message and exits.

=item B<--man>

Prints the manual page and exits.

=back

=head1 DESCRIPTION

B<This program> is a module of ABT (Another Backup Tool).

=head1 RETURN CODE

	Return Code :


=cut